#!ipxe

set menu-timeout 60000
iseq ${net0/mac} 00:0c:29:0b:46:a2 && set menu-default create || set menu-default join
set base-url http://192.168.1.79/ipxe

:start
  menu iPXE Boot Menu --${mac}--${ip}
  
  item --gap --             -------------------------------- Harvester ---------------------------
  item create				Create a new cluster
  item join				Join an existing cluster
  item --gap --             -------------------------------- Advanced -----------------------------
  item --key l local            [L] Boot from local drive
  item --key s shell 		[S] Drop to iPXE Shell
  item --key r reboot		[R] Reboot the Computer

  choose --timeout ${menu-timeout} --default ${menu-default} selected
  goto ${selected}

:create
  kernel ${base-url}/harvester-vmlinuz-amd64 k3os.mode=install k3os.debug console=tty1 harvester.install.automatic=true harvester.install.config_url=${base-url}/config-create.yaml
  initrd ${base-url}/harvester-initrd-amd64
  boot

:join
  kernel ${base-url}/harvester-vmlinuz-amd64 k3os.mode=install k3os.debug console=tty1 harvester.install.automatic=true harvester.install.config_url=${base-url}/config-join.yaml
  initrd ${base-url}/harvester-initrd-amd64
  boot

:local
  sanboot --no-describe --drive 0x80

:shell
  echo Type 'exit' to go back to the menu.
  shell
  goto start

:reboot
  reboot

:exit
  exit
